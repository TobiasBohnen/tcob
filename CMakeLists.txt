cmake_minimum_required(VERSION 3.25.0)
project(tcob LANGUAGES C CXX VERSION 0.0.1)

set(CMAKE_POSITION_INDEPENDENT_CODE ON)

if(NOT TCOB_IS_CI AND NOT EMSCRIPTEN)
  if(CMAKE_BUILD_TYPE STREQUAL "Debug")
    set(CMAKE_INTERPROCEDURAL_OPTIMIZATION FALSE)
  else()
    set(CMAKE_INTERPROCEDURAL_OPTIMIZATION TRUE)
  endif()
endif()

set(TCOB_INC_DIR ${CMAKE_CURRENT_SOURCE_DIR}/tcob/include)
include(cmake/TcobFunctions.cmake)

# options######################################################################
option(TCOB_BUILD_EXAMPLES "Build extras" ON)
option(TCOB_BUILD_GAMES "Build games" ON)
option(TCOB_BUILD_TESTS "Build tests" ON)
option(TCOB_BUILD_BENCHMARKS "Build benchmarks" OFF)
option(TCOB_BUILD_TOOLS "Build tools" ON)
option(TCOB_BUILD_SHARED "Build shared library" OFF)

if(TCOB_BUILD_SHARED OR BUILD_SHARED_LIBS)
  set(TCOB_BUILD_SHARED ON CACHE BOOL "Build shared library" FORCE)
else()
  set(TCOB_BUILD_SHARED OFF CACHE BOOL "Build shared library" FORCE)
endif()

option(TCOB_ENABLE_RENDERER_OPENGL45 "" ON)
option(TCOB_ENABLE_RENDERER_OPENGLES30 "" ON)
option(TCOB_ENABLE_RENDERER_NULL "" ON)

option(TCOB_ENABLE_ADDON_PHYSICS_BOX2D "" ON)
option(TCOB_ENABLE_ADDON_AUDIO_SPEECH "" ON)
option(TCOB_ENABLE_ADDON_AUDIO_TINYSOUNDFONT "" ON)
option(TCOB_ENABLE_ADDON_SCRIPTING_LUA "" ON)
option(TCOB_ENABLE_ADDON_SCRIPTING_SQUIRREL "" ON)
option(TCOB_ENABLE_ADDON_DATA_SQLITE "" ON)
option(TCOB_ENABLE_ADDON_GFX_LITEHTML "" ON)

option(TCOB_ENABLE_FILETYPES_AUDIO_DRLIBS "" ON)
option(TCOB_ENABLE_FILETYPES_AUDIO_VORBIS "" ON)
option(TCOB_ENABLE_FILETYPES_AUDIO_LIBXMP "" ON)
option(TCOB_ENABLE_FILETYPES_AUDIO_OPUS "" ON)
option(TCOB_ENABLE_FILETYPES_GFX_QOI "" ON)
option(TCOB_ENABLE_FILETYPES_GFX_WEBP "" ON)
option(TCOB_ENABLE_FILETYPES_GFX_THEORA "" ON)

option(TCOB_MINIMAL "Disable all optional dependencies" OFF)

if(TCOB_MINIMAL)
  set(TCOB_ENABLE_RENDERER_OPENGLES30 OFF CACHE BOOL "" FORCE)

  set(TCOB_ENABLE_ADDON_PHYSICS_BOX2D OFF CACHE BOOL "" FORCE)
  set(TCOB_ENABLE_ADDON_AUDIO_SPEECH OFF CACHE BOOL "" FORCE)
  set(TCOB_ENABLE_ADDON_AUDIO_TINYSOUNDFONT OFF CACHE BOOL "" FORCE)
  set(TCOB_ENABLE_ADDON_SCRIPTING_LUA OFF CACHE BOOL "" FORCE)
  set(TCOB_ENABLE_ADDON_SCRIPTING_SQUIRREL OFF CACHE BOOL "" FORCE)
  set(TCOB_ENABLE_ADDON_DATA_SQLITE OFF CACHE BOOL "" FORCE)
  set(TCOB_ENABLE_ADDON_GFX_LITEHTML OFF CACHE BOOL "" FORCE)

  set(TCOB_ENABLE_FILETYPES_AUDIO_DRLIBS OFF CACHE BOOL "" FORCE)
  set(TCOB_ENABLE_FILETYPES_AUDIO_VORBIS OFF CACHE BOOL "" FORCE)
  set(TCOB_ENABLE_FILETYPES_AUDIO_LIBXMP OFF CACHE BOOL "" FORCE)
  set(TCOB_ENABLE_FILETYPES_AUDIO_OPUS OFF CACHE BOOL "" FORCE)
  set(TCOB_ENABLE_FILETYPES_GFX_QOI OFF CACHE BOOL "" FORCE)
  set(TCOB_ENABLE_FILETYPES_GFX_WEBP OFF CACHE BOOL "" FORCE)
  set(TCOB_ENABLE_FILETYPES_GFX_THEORA OFF CACHE BOOL "" FORCE)
endif()

if(NOT TCOB_ENABLE_ADDON_PHYSICS_BOX2D
  OR NOT TCOB_ENABLE_ADDON_AUDIO_SPEECH
  OR NOT TCOB_ENABLE_ADDON_AUDIO_TINYSOUNDFONT
  OR NOT TCOB_ENABLE_ADDON_SCRIPTING_LUA
  OR NOT TCOB_ENABLE_ADDON_SCRIPTING_SQUIRREL
  OR NOT TCOB_ENABLE_ADDON_DATA_SQLITE
  OR NOT TCOB_ENABLE_ADDON_GFX_LITEHTML)
  set(TCOB_BUILD_EXAMPLES OFF CACHE BOOL "" FORCE)
  set(TCOB_BUILD_GAMES OFF CACHE BOOL "" FORCE)
endif()

# compile options##############################################################
if(WIN32)
  add_compile_definitions(_ITERATOR_DEBUG_LEVEL=0)
endif()

if(MSVC)
  add_compile_options(/utf-8)
  add_compile_options(/bigobj)
  add_compile_options(/arch:AVX2)
  add_compile_definitions(_CRT_SECURE_NO_WARNINGS)

  if(NOT TCOB_IS_CI)
    set(CMAKE_MSVC_RUNTIME_LIBRARY "MultiThreaded$<$<CONFIG:Debug>:Debug>")
  endif()
endif()

if(EMSCRIPTEN)
  set(TCOB_ENABLE_RENDERER_OPENGLES30 ON CACHE BOOL "" FORCE)
  set(TCOB_ENABLE_RENDERER_OPENGL45 OFF CACHE BOOL "" FORCE)
  set(TCOB_BUILD_TESTS OFF CACHE BOOL "" FORCE)
  set(TCOB_BUILD_BENCHMARKS OFF CACHE BOOL "" FORCE)
  set(TCOB_BUILD_TOOLS OFF CACHE BOOL "" FORCE)
  set(CMAKE_EXECUTABLE_SUFFIX ".html")
endif()

# subdirs######################################################################
set(TCOB_LIBS "")
add_subdirectory(tcob)

if(PROJECT_IS_TOP_LEVEL)
  # ctest
  set_property(GLOBAL PROPERTY CTEST_TARGETS_ADDED 1)
  option(BUILD_TESTING "" OFF)
  include(CTest)
  enable_testing()

  if(TCOB_BUILD_TESTS)
    add_subdirectory(extras/tests)
  endif()

  if(TCOB_BUILD_BENCHMARKS)
    add_subdirectory(extras/benchmarks)
  endif()

  if(TCOB_BUILD_TOOLS)
    add_subdirectory(extras/tools)
  endif()

  if(TCOB_BUILD_EXAMPLES)
    add_subdirectory(extras/examples)
  endif()

  if(TCOB_BUILD_GAMES)
    add_subdirectory(extras/games)
  endif()
endif()

# status#######################################################################
MESSAGE(STATUS "++++++++++++++++")
MESSAGE(STATUS "Build tests: " ${TCOB_BUILD_TESTS})
MESSAGE(STATUS "Build benchmarks: " ${TCOB_BUILD_BENCHMARKS})
MESSAGE(STATUS "Build tools: " ${TCOB_BUILD_TOOLS})
MESSAGE(STATUS "Build examples: " ${TCOB_BUILD_EXAMPLES})
MESSAGE(STATUS "Build games: " ${TCOB_BUILD_GAMES})
MESSAGE(STATUS "++++++++++++++++")
MESSAGE(STATUS "Build shared: " ${TCOB_BUILD_SHARED})
MESSAGE(STATUS "++++++++++++++++")
MESSAGE(STATUS "renderers:")
MESSAGE(STATUS "  OpenGL45: " ${TCOB_ENABLE_RENDERER_OPENGL45})
MESSAGE(STATUS "  OpenGLES30: " ${TCOB_ENABLE_RENDERER_OPENGLES30})
MESSAGE(STATUS "  null: " ${TCOB_ENABLE_RENDERER_NULL})
MESSAGE(STATUS "++++++++++++++++")
MESSAGE(STATUS "addons:")
MESSAGE(STATUS "physics:")
MESSAGE(STATUS "  Box2D: " ${TCOB_ENABLE_ADDON_PHYSICS_BOX2D})
MESSAGE(STATUS "audio:")
MESSAGE(STATUS "  Speech: " ${TCOB_ENABLE_ADDON_AUDIO_SPEECH})
MESSAGE(STATUS "  TinySoundFont: " ${TCOB_ENABLE_ADDON_AUDIO_TINYSOUNDFONT})
MESSAGE(STATUS "scripting:")
MESSAGE(STATUS "  Lua: " ${TCOB_ENABLE_ADDON_SCRIPTING_LUA})
MESSAGE(STATUS "  Squirrel: " ${TCOB_ENABLE_ADDON_SCRIPTING_SQUIRREL})
MESSAGE(STATUS "data:")
MESSAGE(STATUS "  SQLite: " ${TCOB_ENABLE_ADDON_DATA_SQLITE})
MESSAGE(STATUS "gfx:")
MESSAGE(STATUS "  litehtml: " ${TCOB_ENABLE_ADDON_GFX_LITEHTML})
MESSAGE(STATUS "++++++++++++++++")
MESSAGE(STATUS "file types:")
MESSAGE(STATUS "audio:")
MESSAGE(STATUS "  DR_libs (.wav, .mp3, .flac): " ${TCOB_ENABLE_FILETYPES_AUDIO_DRLIBS})
MESSAGE(STATUS "  libvorbis (.ogg): " ${TCOB_ENABLE_FILETYPES_AUDIO_VORBIS})
MESSAGE(STATUS "  libopus (.opus): " ${TCOB_ENABLE_FILETYPES_AUDIO_OPUS})
MESSAGE(STATUS "  libxmp (.it, .mod, .s3m, .xm): " ${TCOB_ENABLE_FILETYPES_AUDIO_LIBXMP})
MESSAGE(STATUS "gfx:")
MESSAGE(STATUS "  qoi (.qoi): " ${TCOB_ENABLE_FILETYPES_GFX_QOI})
MESSAGE(STATUS "  webp (.webp): " ${TCOB_ENABLE_FILETYPES_GFX_WEBP})
MESSAGE(STATUS "  theora (.ogv): " ${TCOB_ENABLE_FILETYPES_GFX_THEORA})
MESSAGE(STATUS "++++++++++++++++")